# Copyright (c) 2018. Mount Sinai School of Medicine
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""
This contains GATK-related processing rules.
"""

rule mark_dups:
  input:
    "{prefix}_merged_aligned_coordinate_sorted.bam"
  output:
    "{prefix}_aligned_coordinate_sorted_dups.bam"
  params:
    mem_gb = _mem_gb_for_ram_hungry_jobs()
  benchmark:
    "{prefix}_mark_dups_benchmark.txt"
  log:
    "{prefix}_mark_dups.log"
  resources:
    mem_mb = _mem_gb_for_ram_hungry_jobs() * 1024
  shell:
    "TMPDIR={wildcards.prefix}_tmp "
    "MAX_SEQUENCES_FOR_DISK_READ_ENDS_MAP=50000 "
    "MAX_FILE_HANDLES_FOR_READ_ENDS_MAP=20000 "
    "SORTING_COLLECTION_SIZE_RATIO=0.250000 "
    "picard -Xmx{params.mem_gb}g -Djava.io.tmpdir={wildcards.prefix}_tmp "
    "MarkDuplicates "
    "INPUT={input} OUTPUT={output} "
    "VALIDATION_STRINGENCY=LENIENT METRICS_FILE={wildcards.prefix}_markdups_metrics.txt "
    "2> {log}"

rule sambamba_index_bam:
  input:
    "{prefix}.bam"
  output:
    "{prefix}.bam.bai"
  threads: _get_half_cores
  benchmark:
    "{prefix}_index_benchmark.txt"
  log:
    "{prefix}_index.log"
  shell:
    "sambamba index -t {threads} {input} {output} 2> {log}"

# TODO(julia): figure out how to combine with RNA IndelRealigner rules, very similar

# if this rule is triggered with "chr" mapping to a chromosome in the _CHR list, it'll be run
# for that chromosome alone. If "chr" matches any other string, IndelRealigner will run for all
# chromosomes.
rule dna_indel_realigner_target_creator_per_chr:
  input:
    expand("{{prefix}}/{type}_aligned_coordinate_sorted_dups.bam.bai", type=["normal", "tumor"]),
    normal = "{prefix}/normal_aligned_coordinate_sorted_dups.bam",
    tumor = "{prefix}/tumor_aligned_coordinate_sorted_dups.bam",
    reference_fai = rules.samtools_index_reference.output,
    reference_dict = rules.picard_sequence_dict_reference.output,
    reference = config["reference"]["genome"]
  output:
    "{prefix}/aligned_coordinate_sorted_dups_indelreal_{chr}.intervals"
  params:
    mem_gb = _mem_gb_for_ram_hungry_jobs()
  threads: _get_half_cores
  benchmark:
    "{prefix}/dna_indel_realigner_target_creator_{chr}_benchmark.txt"
  log:
    "{prefix}/dna_indel_realigner_target_creator_{chr}.log"
  resources:
    mem_mb = _mem_gb_for_ram_hungry_jobs() * 1024
  run:
    intervals_str = _get_intervals_str(wildcards)
    shell("""
      gatk -Xmx{params.mem_gb}g -T RealignerTargetCreator -R {input.reference} \
      %s \
      -I {input.normal} -I {input.tumor} -o {output} -nt {threads} \
      --filter_reads_with_N_cigar --filter_mismatching_base_and_quals --filter_bases_not_stored \
      2> {log}
    """ % intervals_str)

# if this rule is triggered with "chr" mapping to a chromosome in the _CHR list, it'll be run
# for that chromosome alone. If "chr" matches any other string, IndelRealigner will run for all
# chromosomes.
rule dna_indel_realigner_per_chr:
  input:
    expand("{{prefix}}/{type}_aligned_coordinate_sorted_dups.bam.bai", type=["normal", "tumor"]),
    normal = "{prefix}/normal_aligned_coordinate_sorted_dups.bam",
    tumor = "{prefix}/tumor_aligned_coordinate_sorted_dups.bam",
    intervals = "{prefix}/aligned_coordinate_sorted_dups_indelreal_{chr}.intervals",
    reference = config["reference"]["genome"]
  output:
    "{prefix}/normal_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam",
    "{prefix}/tumor_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam"
  params:
    mem_gb = _mem_gb_for_ram_hungry_jobs()
  benchmark:
    "{prefix}/dna_indel_realigner_{chr}_benchmark.txt"
  log:
    "{prefix}/dna_indel_realigner_{chr}.log"
  resources:
    mem_mb = _mem_gb_for_ram_hungry_jobs() * 1024
  # IndelRealigner writes the output to this directory; need to move the files manually after
  run:
    intervals_str = _get_intervals_str(wildcards)
    shell("""
      gatk -Xmx{params.mem_gb}g -T IndelRealigner -compress 0 -R {input.reference} \
      -I {input.normal} -I {input.tumor} \
      -targetIntervals {input.intervals} %s \
      --filter_reads_with_N_cigar --filter_mismatching_base_and_quals --filter_bases_not_stored \
      --nWayOut _indelreal_chr_{wildcards.chr}.bam \
      2> {log} && \
      mv {{normal,tumor}}_aligned_coordinate_sorted_dups_indelreal_chr_{wildcards.chr}.bam \
      {wildcards.prefix} && \
      mv {{normal,tumor}}_aligned_coordinate_sorted_dups_indelreal_chr_{wildcards.chr}.bai \
      {wildcards.prefix}
    """ % intervals_str)

def _get_dna_indel_realigner_input(wildcards):
  if _PARALLEL_INDEL_REALIGNER:
    return ["%s_aligned_coordinate_sorted_dups_indelreal_chr_%s.bam" % (
        wildcards.prefix, chromosome) for chromosome in _CHR]
  else:
    return "%s_aligned_coordinate_sorted_dups_indelreal_chr_ALL.bam" % wildcards.prefix


if _PARALLEL_INDEL_REALIGNER:
  rule parallel_dna_indel_realigner:
    input:
      bam = dynamic("{prefix}_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam")
      # bam = expand("{{prefix}}_aligned_coordinate_sorted_dups_indelreal_chr_{chr}.bam", chr=_CHR)
      # contig_names = rules.extract_contig_names.output
    output:
      bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam"
    benchmark:
      "{prefix}_indel_realigner_benchmark.txt"
    log:
      "{prefix}_indel_realigner.log"
    shell:
      "sambamba merge {output.bam} {input.bam}"
else:
  rule dna_indel_realigner:
    input:
      bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal_chr_ALL.bam"
    output:
      bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam"
    benchmark:
      "{prefix}_indel_realigner_benchmark.txt"
    log:
      "{prefix}_indel_realigner.log"
    run:
      shell("mv {input.bam} {output.bam}")
      input_bai = input.bam.replace(".bam", ".bai")
      output_bai = output.bam.replace(".bam", ".bai")
      # also need to rename the .bai from the .bam
      shell("mv %s %s" % (input_bai, output_bai))

# rule dna_indel_realigner:
#   input:
#     bam = _get_dna_indel_realigner_input,
#     contig_names = rules.extract_contig_names.output
#   output:
#     bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam"
#   benchmark:
#     "{prefix}_indel_realigner_benchmark.txt"
#   log:
#     "{prefix}_indel_realigner.log"
#   run:
#     if _PARALLEL_INDEL_REALIGNER:
#       shell("sambamba merge {output.bam} {input.bam}")
#     else:
#       shell("mv {input.bam} {output.bam}")
#       input_bai = input.bam.replace(".bam", ".bai")
#       output_bai = output.bam.replace(".bam", ".bai")
#       # also need to rename the .bai from the .bam
#       shell("mv %s %s" % (input_bai, output_bai))

rule base_recalibrator:
  input:
    bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam",
    reference = config["reference"]["genome"],
    known_sites = config["reference"]["dbsnp"]
  output:
    "{prefix}_aligned_coordinate_sorted_dups_indelreal_bqsr.table"
  params:
    mem_gb = _mem_gb_for_ram_hungry_jobs()
  threads: _get_half_cores
  benchmark:
    "{prefix}_base_recalibrator_benchmark.txt"
  log:
    "{prefix}_base_recalibrator.log"
  resources:
    mem_mb = _mem_gb_for_ram_hungry_jobs() * 1024
  shell:
    "gatk -Xmx{params.mem_gb}g "
    "-T BaseRecalibrator -nct {threads} -R {input.reference} -I {input.bam} "
    "-knownSites {input.known_sites} -o {output} 2> {log}"

rule bqsr_print_reads:
  input:
    bam = "{prefix}_aligned_coordinate_sorted_dups_indelreal.bam",
    bqsr = "{prefix}_aligned_coordinate_sorted_dups_indelreal_bqsr.table",
    reference = config["reference"]["genome"]
  output:
    "{prefix}_aligned_coordinate_sorted_dups_indelreal_bqsr.bam"
  params:
    mem_gb = _mem_gb_for_ram_hungry_jobs()
  threads: _get_half_cores
  benchmark:
    "{prefix}_base_recalibrator_print_reads_benchmark.txt"
  log:
    "{prefix}_base_recalibrator_print_reads.log"
  resources:
    mem_mb = _mem_gb_for_ram_hungry_jobs() * 1024
  shell:
    "gatk -Xmx{params.mem_gb}g "
    "-T PrintReads -nct {threads} -R {input.reference} -I {input.bam} -BQSR {input.bqsr} "
    "-o {output} 2> {log}"
