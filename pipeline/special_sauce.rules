# Copyright (c) 2018. Mount Sinai School of Medicine
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""
This contains rules for our own data processing libraries.
"""

from os.path import join

rule pyensembl:
  output:
    touch("$PYENSEMBL_CACHE_DIR/homo_sapiens_75.cached"),
    touch("$PYENSEMBL_CACHE_DIR/mus_musculus_93.cached")
  log:
    join(LOGDIR, "pyensembl_install.log")
  shell:
    "pyensembl install --release 75 --species 'homo sapiens' >> {log} 2>&1 && "
    "pyensembl install --release 93 --species 'mus musculus' >> {log} 2>&1"

def _get_vaxrank_input_vcfs(wildcards):
    return [join(WORKDIR, "%s.vcf" % vcf_type) for vcf_type in config["variant_callers"]]

def _check_vaxrank_wildcards(wildcards):
    if wildcards.mhc_predictor != config["mhc_predictor"]:
        raise ValueError("Vaccine peptide report output filenames must match MHC predictor "
            "specified in config: %s" % config["mhc_predictor"])
    if wildcards.vcf_types != "-".join(config["variant_callers"]):
        raise ValueError("Vaccine peptide report output filenames must match variant callers "
            "specified in config: %s" % config["variant_callers"])

# only define the Vaxrank rule if we have MHC alleles specified in the config
# TODO(julia): support inferring MHC alleles from seq2hla instead of requiring user input
if "mhc_alleles" in config["input"]:
    rule vaxrank:
      input:
        vcfs = _get_vaxrank_input_vcfs,
        rna = join(WORKDIR, "rna_final_sorted.bam"),
        rna_index = join(WORKDIR, "rna_final_sorted.bam.bai"),
        pyensembl_cache = "$PYENSEMBL_CACHE_DIR/homo_sapiens_75.cached"
      output:
        ascii_report = join(WORKDIR, "vaccine-peptide-report_{mhc_predictor}_{vcf_types}.txt"),
        json_file = join(WORKDIR, "vaccine-peptide-report_{mhc_predictor}_{vcf_types}.json"),
        pdf_report = join(WORKDIR, "vaccine-peptide-report_{mhc_predictor}_{vcf_types}.pdf"),
        xlsx_report = join(WORKDIR, "vaccine-peptide-report_{mhc_predictor}_{vcf_types}.xlsx"),
        all_passing_variants = join(WORKDIR, "all-passing-variants_{mhc_predictor}_{vcf_types}.csv")
      params:
        mhc_alleles = config["input"]["mhc_alleles"],
        patient_id = config["input"]["id"],
        vaccine_peptide_length = 25,
        padding_around_mutation = 5,
        max_vaccine_peptides_per_mutation = 3,
        min_mapping_quality = 1,
        min_variant_sequence_coverage = 1,
        min_alt_rna_reads = 2,
        mhc_epitope_lengths = "8-11"
      benchmark:
        join(BENCHMARKDIR, "vaxrank_{mhc_predictor}_{vcf_types}.txt")
      log:
        join(LOGDIR, "vaxrank_{mhc_predictor}_{vcf_types}.log")
      run:
        _check_vaxrank_wildcards(wildcards)
        vcf_input_str = ' '.join(['--vcf %s' % x for x in input.vcfs])
        shell("""
            vaxrank %s \
            --bam {input.rna} \
            --mhc-predictor {wildcards.mhc_predictor} \
            --mhc-alleles %s \
            --output-ascii-report {output.ascii_report} \
            --output-pdf-report {output.pdf_report} \
            --output-xlsx-report {output.xlsx_report} \
            --output-json-file {output.json_file} \
            --output-passing-variants-csv {output.all_passing_variants} \
            --output-patient-id {params.patient_id} \
            --log-path {log} \
            --vaccine-peptide-length {params.vaccine_peptide_length} \
            --padding-around-mutation {params.padding_around_mutation} \
            --max-vaccine-peptides-per-mutation {params.max_vaccine_peptides_per_mutation} \
            --min-mapping-quality {params.min_mapping_quality} \
            --min-variant-sequence-coverage {params.min_variant_sequence_coverage} \
            --min-alt-rna-reads {params.min_alt_rna_reads} \
            --mhc-epitope-lengths {params.mhc_epitope_lengths}
            """ % (vcf_input_str, ",".join(params.mhc_alleles)))
