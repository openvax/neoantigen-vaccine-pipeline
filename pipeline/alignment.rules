# Copyright (c) 2018. Mount Sinai School of Medicine
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""
This contains alignment-related processing rules.
"""

def _get_read_group_header(wildcards):
  prefix = wildcards.prefix
  basename = os.path.basename(prefix)
  if basename.startswith("normal"):
    sample = "normal"
  elif basename.startswith("tumor"):
    sample = "tumor"
  else:
    raise ValueError("Unexpected prefix, cannot extract SM tag: %s" % prefix)
  patient_id = config["input"]["id"]
  sample_id = "%s_%s" % (patient_id, sample)
  library = sample_id
  return "\\t".join([
      "@RG",
      "ID:%s" % prefix,
      "SM:%s" % sample_id,
      "LB:%s" % library,
      "PL:Illumina"
  ])

# TODO(julia): if we need to bring back alignment for BAMs, make something similar to this and run
# "samtools fastq <input.bam> | <bwa command>" where the bwa command is identical to this one except
# also uses the -p param
# see https://www.biostars.org/p/134638/
#
# NB: this will not work correctly on a paired-end interleaved FASTQ input
rule bwa_mem_single_end:
  input:
    r = "{prefix}.fastq.gz",
    reference = config["reference"]["genome"],
    done = config["reference"]["genome"] + ".done"
  output:
    "{prefix}_aligned.sam"
  params:
    rg = _get_read_group_header
  resources:
    mem_mb = _mem_gb_for_alignment() * 1024
  benchmark:
    "{prefix}_bwa_mem_benchmark.txt"
  log:
    "{prefix}_bwa_mem.log"
  threads: _get_threads_for_alignment
  shell:
    "bwa mem -R '{params.rg}' -M -t {threads} -O 6 -E 1 -B 4 "
    "{input.reference} {input.r} "
    "> {output} 2> {log}"

rule bwa_mem_paired_end:
  input:
    r1 = "{prefix}_R1.fastq.gz",
    r2 = "{prefix}_R2.fastq.gz",
    reference = config["reference"]["genome"],
    done = config["reference"]["genome"] + ".done"
  output:
    "{prefix}_aligned.sam"
  params:
    rg = _get_read_group_header
  resources:
    mem_mb = _mem_gb_for_alignment() * 1024
  benchmark:
    "{prefix}_bwa_mem_benchmark.txt"
  log:
    "{prefix}_bwa_mem.log"
  threads: _get_threads_for_alignment
  shell:
    "bwa mem -R '{params.rg}' -M -t {threads} -O 6 -E 1 -B 4 "
    "{input.reference} {input.r1} {input.r2} "
    "> {output} 2> {log}"

rule convert_alignment_to_sorted_bam:
  input:
    "{prefix}_aligned.sam"
  output:
    "{prefix}_aligned_coordinate_sorted.bam"
  params:
    mem_gb = _mem_gb_for_ram_hungry_jobs()
  benchmark:
    "{prefix}_convert_alignment_to_sorted_bam_benchmark.txt"
  log:
    "{prefix}_convert_alignment_to_sorted_bam.log"
  resources:
    mem_mb = _mem_gb_for_ram_hungry_jobs() * 1024
  shell:
    "picard -Xmx{params.mem_gb}g "
    "SortSam INPUT={input} OUTPUT={output} SORT_ORDER=coordinate 2> {log}"
